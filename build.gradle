buildscript {
    repositories {
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.4'
        classpath "gradle.plugin.io.netifi:gradle-flatbuffers-plugin:1.0.4"
    }
}

plugins {
    id "com.jfrog.bintray" version "1.7"
}

apply plugin: 'java'  // This is the backbone of the gradle build process, we don't just use it for java files.
apply plugin: 'maven-publish'  // Needed to get jitpack to work.
apply plugin: 'io.netifi.flatbuffers'

repositories {
    jcenter()
}

dependencies {
    compile group: 'net.java.dev.jna', name: 'jna', version: '4.5.1'  // Allows the java bot manager to communicate with dll
    compile group: 'net.java.dev.jna', name: 'jna-platform', version: '4.5.1'  // Allows the java bot manager to communicate with dll
    compile group: 'net.sf.py4j', name: 'py4j', version: '0.10.6'  // Allows the java bot manager to accept commands from python.
    compile 'com.github.davidmoten:flatbuffers-java:1.9.0.1'  // Temporarily using this guy's flatbuffers package because the official one is behind.
    testRuntime files('src/main/python/RLBotFramework/dll')
}

project.setGroup('org.rlbot.commons')
project.setVersion('0.0.1')

import io.netifi.flatbuffers.plugin.tasks.FlatBuffers
def parentFolder = new File(new File('.').getCanonicalPath()).getParentFile().absolutePath
def defaultCoreFolder = 'rlbot-core'
def pythonRoot = './src/main/python/'

sourceSets {
    main {
        java {
            // This is the default location where java protobuf classes are generated
            srcDirs = ['src/main/java', 'src/generated/java/flatbuffers']
        }
    }
}

// After protobuf is generated, copy the generated file to its home in the python folder.
task copyFlatbuffersPython(type: Copy) {
    dependsOn 'generateFlatbuffersPython'
    from fileTree('src/generated/python/flatbuffers/rlbot')
    into pythonRoot + 'RLBotMessages'
}

flatbuffers {
    flatcPath = 'src/main/flatbuffers'
}

task generateFlatbuffersJava(type: FlatBuffers) {
    inputDir = file("src/main/flatbuffers")
    outputDir = file("src/generated/java/flatbuffers")
    language = 'java'
}

task generateFlatbuffersPython(type: FlatBuffers) {
    inputDir = file("src/main/flatbuffers")
    outputDir = file("src/generated/python/flatbuffers")
    language = 'python'
}

task generateFlatbuffersCpp(type: FlatBuffers) {
    inputDir = file("src/main/flatbuffers")
    outputDir = file("src/generated/cpp/flatbuffers")
    language = 'cpp'
}

// Useful standalone task for setting up visual studio development environment
task assembleProtos {
    dependsOn 'copyFlatbuffersPython'
    dependsOn 'generateFlatbuffersJava'
    dependsOn 'generateFlatbuffersCpp'
}

def counter = 0
String regex = /(?i)rlbot[-_ ]core/
new File(parentFolder).eachDir{
    if (it.name ==~ regex) {
        if (counter > 0) {
            throw new Exception('More than one core directory found please move one of them.')
        }
        defaultCoreFolder = it.absolutePath
        print('Found dll folder ')
        println(defaultCoreFolder)
        counter++
    }
}

// This is just a convenience task for developers.
task copyDlls(type: Copy) {
    from files('src/main/cpp/RLBotInterface/RLBotInterface/Bin/x64/Debug/RLBotInterface.dll',
            defaultCoreFolder + '/RLBot Core/Bin/Win32/Debug-SDK/RLBot Core.dll')
    into pythonRoot + 'RLBotFramework/dll'
    rename('RLBot Core.dll', 'RLBot_Core.dll')
    rename('RLBotInterface.dll', 'RLBot_Core_Interface.dll')
}

// You can use the clean task to remove build output.
clean {
    // In addition to the normal clean behavior, also remove the generated flatbuffer file(s).
    delete pythonRoot + 'RLBotMessages/flat'
    delete 'src/generated'
}

// Use pip, which is the python package manager, to install all requirements listed in requirements.txt.
// Python and pip must already be installed for this to work.
task pipInstallRequirements(type: Exec) {
    commandLine "pip", "install", "-r", pythonRoot + "requirements.txt"
}

// Establish some task dependencies so that some tasks always run before others.
compileJava.dependsOn assembleProtos


// This task creates a jar file which contains the java source files.
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

// Parses a .gitignore file and attempts to build a list of file paths that can be used by gradle.
def gitignoreToList(File f) {
    def ignores = []
    f.eachLine { line ->
        //ignore comments and empty lines
        if (!line.startsWith('#') && !line.isEmpty()) {
            ignores.add(line)
        }
    }
    return ignores
}

// Creates a zip file with all the python files needed for running rlbot. Custom bot repositories should be able to
// depend on this zip instead of keeping a local copy of framework files.
task pythonZip(type: Zip, dependsOn: processResources) {
    into('/') {
        from(fileTree('src/main/python/'))   // Grab all the files in the src/main/python folder and add them at the root level
        from 'LICENSE.txt'   // Also grab the license
        exclude gitignoreToList(file('.gitignore'))  // Avoid putting any git-ignored files in the zip
        exclude '**/__pycache__'   // Explicitly exclude __pycache__ folders because the gitignoreToList trick doesn't cover this
    }
}

// This is associated with the maven-publish plugin. It's needed for jitpack to work properly.
publishing {
    publications {
        Main(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact (pythonZip) {
                classifier = 'python'
            }
            groupId project.getGroup()
            artifactId 'framework'
            version project.getVersion()
        }
    }
}

// In order to run the bintrayUpload task successfully, create a file called local.properties
// and add a line like bintray.apikey=...
// To get the API key, go here and log in: https://bintray.com/profile/edit/organizations
// You'll also need to increment the project.setVersion(...) number in this file to push successfully.
Properties localProperties = new Properties()
File propsFile = new File('local.properties')
if (propsFile.exists()) {
    localProperties.load(new FileInputStream(propsFile))
}

bintray {
    user = 'rlbotofficial'
    key = localProperties.getProperty("bintray.apikey")
    publications = ['Main']
    pkg {
        repo = 'RLBotMaven'
        name = 'rlbot-framework'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/RLBot/RLBot.git'
        version {
            name = project.getVersion()
            released = new Date()
        }
    }
}
